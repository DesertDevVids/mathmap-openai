/*
 * scanner.fl
 *
 * MathMap
 *
 * Copyright (C) 1997-2000 Mark Probst
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

%{
#include <string.h>

#include "exprtree.h"
#include "builtins.h"
#include "parser.h"
%}

%option noyywrap

%%

if                          return T_IF;
then                        return T_THEN;
else                        return T_ELSE;
end                         return T_END;
while                       return T_WHILE;
do                          return T_DO;
xor                         return T_XOR;
[a-zA-Z_][a-zA-Z0-9_]*      {
			        strncpy(mmlval.ident, mmtext, MAX_IDENT_LENGTH);
				mmlval.ident[MAX_IDENT_LENGTH] = '\0';
				return T_IDENT;
	                    }
\"[^\"\n]*\"                {
				int len;

				if (mmleng - 2 < MAX_IDENT_LENGTH)
				    len = mmleng - 2;
				else
				    len = MAX_IDENT_LENGTH;
				strncpy(mmlval.ident, mmtext + 1, len);
				mmlval.ident[len] = '\0';
				return T_STRING;
			    }
[0-9]+                      { mmlval.exprtree = make_number(atof(mmtext)); return T_INT; }
[0-9]*\.[0-9]+              { mmlval.exprtree = make_number(atof(mmtext)); return T_FLOAT; }
"=="                        return T_EQUAL;
"<="                        return T_LESSEQUAL;
">="                        return T_GREATEREQUAL;
"!="                        return T_NOTEQUAL;
"||"                        return T_OR;
"&&"                        return T_AND;
"::"                        return T_CONVERT;
\.\.                        return T_RANGE;
[-<>!,()+*/%=;^:\[\]]       return mmtext[0];
#.*                         ;
[ \t\n]                     ;

%%

void
scanFromString (char *string)
{
    mm_scan_string(string);
}

void
endScanningFromString (void)
{
    mm_delete_buffer(YY_CURRENT_BUFFER);
}
